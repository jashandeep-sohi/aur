# Maintainer: Jashandeep Sohi <jashandeep.s.sohi@gmail.com>

pkgname=sabnzbd-git
pkgver=0.7.0.304.ga50412a
pkgrel=1
pkgdesc="Open Source Binary Newsreader written in Python."
arch=('i686' 'x86_64')
url="http://sabnzbd.org/"
license=('GPL')
depends=('python2'
         'python2-cheetah>=2.0.1'
         'par2cmdline>=0.4'
         'unrar>=3.90'
         'unzip>=5.52'
         'python2-yenc>=0.3'
         'python2-pyopenssl>=0.11'
         'openssl>=0.9.8')
makedepends=('git')
conflicts=('sabnzbd')
source=('sabnzbd.systemd')
md5sums=('863dfc3234a6ba76bb20e3e738cd463c')
install='sabnzbd.install'


_gitroot="https://github.com/sabnzbd/sabnzbd.git"
_gitname="sabnzbd"

pkgver() {
  cd $_gitname
  git describe --always | sed 's|-|.|g'
}

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
}

package() {
  cd "$srcdir/$_gitname-build"
  rm -rf win osx NSIS_Installer.nsi make_dmg.py Sample-PostProc.cmd
  
  mkdir -p "$pkgdir/usr/src/$pkgname"
  cp -rf * "$pkgdir/usr/src/$pkgname"
  python2 -OO "$pkgdir/usr/src/$pkgname/SABnzbd.py" --help 1> /dev/null 2> /dev/null
  
  mkdir -p "$pkgdir/usr/bin"
  echo '#!/bin/sh' > "$pkgdir/usr/bin/sabnzbd"
  echo "python2 /usr/src/$pkgname/SABnzbd.py" '"$@"' >> "$pkgdir/usr/bin/sabnzbd"
  chmod 755 "$pkgdir/usr/bin/sabnzbd"
  
  install -Dm 744 "$srcdir/sabnzbd.systemd" "$pkgdir/usr/lib/systemd/system/sabnzbd.service"
    
}

# vim:set ts=2 sw=2 et:
